apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.github.ben-manes.versions'

dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

android {

    compileSdkVersion versions.compileSdk
    buildToolsVersion versions.buildTools

    defaultConfig {
        applicationId "io.petros.posts.kotlin"
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode app.major * 10000 + app.minor * 1000 + app.patch * 100 + app.build
        versionName "${app.major}.${app.minor}.${app.patch}"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError true
        checkAllWarnings true
        ignoreWarnings false
        checkReleaseBuilds true
        warningsAsErrors true
        disable 'UnusedResources', 'UnusedIds', 'IconExpectedSize', 'MissingRegistered'
        htmlReport true
        xmlReport true
    }

}

dependencies {

    implementation deps.kotlin.jre
    implementation deps.support.appCompat
    implementation deps.support.constraintLayout
    implementation deps.support.design

    testImplementation deps.test.junit

    androidTestImplementation(deps.androidTest.espresso, {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

}

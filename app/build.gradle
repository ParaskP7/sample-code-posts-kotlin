apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.github.ben-manes.versions'

apply from: 'config/gradle/quality.gradle'
apply from: 'config/gradle/jacoco.gradle'

dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

android {

    compileSdkVersion versions.compileSdk
    buildToolsVersion versions.buildTools

    defaultConfig {
        applicationId "io.petros.posts.kotlin"
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode app.major * 10000 + app.minor * 1000 + app.patch * 100 + app.build
        versionName "${app.major}.${app.minor}.${app.patch}"
        vectorDrawables.useSupportLibrary = true // For vector image support.
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            minifyEnabled false
        }
    }

    dataBinding {
        enabled = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

}

dependencies {

    implementation deps.kotlin.jre
    implementation deps.support.appCompat
    implementation deps.support.constraintLayout
    implementation deps.support.design
    implementation deps.support.cardView
    kapt deps.databinding.compiler
    implementation deps.arch.lifecycleRuntime
    implementation deps.arch.lifecycleExtensions
    kapt deps.arch.lifecycleCompiler
    implementation deps.arch.roomRuntime
    kapt deps.arch.roomCompiler
    implementation deps.arch.roomRx
    implementation deps.kodein.runtime
    implementation deps.kodein.android
    implementation deps.butterKnife.runtime
    kapt deps.butterKnife.compiler
    implementation deps.rx.java
    implementation deps.rx.android
    debugImplementation deps.rx.lint
    implementation deps.retrofit.runtime
    implementation deps.retrofit.converterGson
    implementation deps.retrofit.rx
    implementation deps.logging.timber
    implementation deps.image.glide
    implementation deps.image.circleView
    implementation deps.other.jodaTime

    debugImplementation deps.leakCanary.op
    releaseImplementation deps.leakCanary.noOp
    testImplementation deps.leakCanary.noOp

    testImplementation deps.test.junit

    androidTestImplementation(deps.androidTest.espresso, {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

}
